;====================
; DIV 割り算を行うサブルーチン
; GR1 / GR2 -> 商は GR2, 余りは GR1
DIV    START
    PUSH    0, GR3
    ST    GR1, A
    ST    GR2, B
    CPA    GR1, =0
    JPL    SKIPA
    XOR    GR1, =#FFFF
    ADDA    GR1, =1
SKIPA    CPA    GR2, =0
    JZE    SKIPD
    JPL    SKIPB
    XOR    GR2, =#FFFF
    ADDA    GR2, =1
SKIPB    LD    GR3, =0
LOOP    CPA    GR1, GR2
    JMI    STEP
    SUBA    GR1, GR2
    LAD    GR3, 1, GR3
    JUMP    LOOP
STEP    LD    GR2, GR3
    LD    GR3, A
    CPA    GR3, =0
    JPL    SKIPC
    XOR    GR1, =#FFFF
    ADDA    GR1, =1
SKIPC    XOR    GR3, B
    CPA    GR3, =0
        JZE     SKIPD
    JPL    SKIPD
    XOR    GR2, =#FFFF
    ADDA    GR2, =1
SKIPD    POP    GR3
    RET
A    DS    1
B    DS    1
     END
;====================
; GR2 の内容（数値データ）を出力装置に書き出すサブルーチン
; このサブルーチンが呼ばれたとき，
; GR7 には，出力用番地の先頭アドレスが，
; GR6 には，現在出力用番地に入っている文字数が，
; それぞれ格納されている．
WRTINT  START
        PUSH    0,GR1 ; GR1の内容をスタックに退避
        PUSH    0,GR2 ; GR2の内容をスタックに退避
    PUSH    0,GR3 ; GR3の内容をスタックに退避
    PUSH    0,GR2 ; 数値データをもう一度スタックに退避
    LD    GR3,=0 ; GR3 はインデックスとして用いる
    ; 数値データが負数である場合は，正の数に変換
    CPA    GR2,=0
    JPL    LOOP1
    XOR    GR2,=#FFFF
    ADDA    GR2,=1
    ; 数値データを変換しながら，バッファに格納
LOOP1    LD    GR1,GR2
    LD    GR2,=10
    CALL    DIV
    XOR    GR1,=#0030
    ST    GR1,BUFFER,GR3
    LAD    GR3,1,GR3
    CPA    GR2,=0
    JNZ    LOOP1
    ; 数値データが負数であれば，'-'を追加
    POP    GR2
    CPA    GR2,=0
    JZE    LOOP2
    JPL    LOOP2
    LD    GR1,='-'
    ST    GR1,BUFFER,GR3
    LAD    GR3,1,GR3
    ; BUFFER を逆順にたどりながら，出力用バッファに格納
LOOP2    LAD    GR3,-1,GR3
    LD    GR1,BUFFER,GR3
    LD    GR2,GR7
    ADDA    GR2,GR6
    ST    GR1,0,GR2
    LAD    GR6,1,GR6
    CPA    GR3,=0
    JNZ    LOOP2
END    POP    GR3
    POP    GR2
    POP    GR1
    RET
BUFFER    DS    6    
    END